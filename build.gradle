buildscript {
  repositories {
    jcenter()
    maven { url = "http://files.minecraftforge.net/maven" }
  }
  dependencies {
    classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
  }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "${mc_version}-${mod_version}"
group = "jackyy.integrationforegoing"
archivesBaseName = "IntegrationForegoing"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
  sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
  version = "${forge_version}"
  runDir = "run"
  mappings = "${mappings_version}"

  replace '@FINGERPRINT@', project.findProperty('signSHA1')
  replaceIn "IntegrationForegoing.java"
}

repositories {
  maven {
    name = "JEI, TCon and Mantle Maven"
    url = "https://dvs1.progwml6.com/files/maven"
  }
  maven {
    name = "HWYLA Maven"
    url = "https://maven.tehnut.info"
  }
  maven {
    name = "TOP Maven"
    url = "https://maven.tterrag.com/"
  }
  maven {
    name = "Curse Maven"
    url = "https://www.cursemaven.com"
  }
}

dependencies {
  deobfCompile "mezz.jei:jei_1.12.2:+"
  deobfCompile "mcp.mobius.waila:Hwyla:1.8.26-B41_1.12.2"
  deobfCompile("mcjty.theoneprobe:TheOneProbe-1.12:+") {
      transitive = false
  }
  deobfCompile "curse.maven:gunpowderlib:2852431"
  deobfCompile "curse.maven:industrialforegoing:2745321"
  deobfCompile("slimeknights:TConstruct:1.12.2-2.+") {
      transitive = false
  }
  deobfCompile "slimeknights.mantle:Mantle:1.12-1.+"
  deobfCompile "curse.maven:immersiveengineering:2799143"
  deobfCompile "curse.maven:infinitylib:2537265"
  deobfCompile "curse.maven:agricraft:2609648"
}

processResources {
  inputs.property "version", project.version
  inputs.property "mcversion", project.minecraft.version
  from(sourceSets.main.resources.srcDirs) {
    include 'mcmod.info'
    expand 'version':project.version, 'mcversion':project.minecraft.version
  }
  from(sourceSets.main.resources.srcDirs) {
    exclude 'mcmod.info'
  }
}

task signJar(type: SignJar, dependsOn: reobfJar) {
  onlyIf {
    project.hasProperty('keyStore')
  }
  keyStore = project.findProperty('keyStore')
  alias = project.findProperty('keyStoreAlias')
  storePass = project.findProperty('keyStorePass')
  keyPass = project.findProperty('keyStoreKeyPass')
  inputFile = jar.archivePath
  outputFile = jar.archivePath
}

build.dependsOn signJar

task deobfJar(type: Jar) {
  from sourceSets.main.output
  classifier = 'deobf'
}

artifacts {
  archives deobfJar
}
